PREFIX mag: <http://ex.org/car-crashes#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
  # Numéro d'intervention
  ?crashId rdf:type mag:Crash;
    # Commune du lieu d'intervention
    dbo:city ?city;
    # Année de l'intervention
    dbo:year ?year;
    # Date système de début d'interv.
    xsd:dateTime ?date;
    # Adresse concaténée
    schema:address ?address;
    # Libellé famille
    mag:crashType ?crashType;
    # Localisation
    schema:geo ?locationId
}

FROM <file:accidents.csv> 

WHERE {
  BIND (URI(CONCAT('http://ex.org/car-crashes/', ?crashIdCol)) AS ?crashId)
  BIND (xsd:integer(?yearCol) AS ?year)
  BIND (xsd:dateTime(?dateCol) AS ?date)
  BIND (xsd:string(?crashTypeCol) AS ?crashType)

  BIND (xsd:integer(?streetNbCol) AS ?streetNb)
  BIND (xsd:string(?streetTypeCol) AS ?streetType)
  BIND (xsd:string(?streetPrefixCol) AS ?streetPrefix)
  BIND (xsd:string(?streetNameCol) AS ?streetName)

  BIND (CONCAT(
    xsd:string(?streetNb),
    " ",
    xsd:string(?streetType),
    " ",
    xsd:string(?streetPrefix),
    " ",
    xsd:string(?streetName)
  ) AS ?address)

  BIND (URI(CONCAT(
    # Add DBPedia URL
    'http://dbpedia.org/resource/',
    REPLACE(
      # Put uppercase only first letter
      CONCAT(
        UCASE(SUBSTR(STR(?cityCol), 1, 1)),
        LCASE(SUBSTR(STR(?cityCol), 2))
      ),
      "\\W", "-", "i"
    )
  )) AS ?city)

  BIND (URI(CONCAT('http://ex.org/car-crashes/', xsd:string(?lat_longCol))) AS ?locationId)
  BIND (xsd:double(REPLACE(?latitudeCol, ",", ".", "i")) AS ?latitude)
  BIND (xsd:double(REPLACE(?longitudeCol, ",", ".", "i")) AS ?longitude)
}